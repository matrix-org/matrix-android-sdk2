import com.vanniktech.maven.publish.SonatypeHost

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    apply from: 'dependencies.gradle'
    apply from: 'dependencies_groups.gradle'

    repositories {
        google()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
    dependencies {
        classpath libs.gradle.gradlePlugin
        classpath libs.gradle.kotlinPlugin
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.32.0'
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:1.8.10"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        maven {
            url 'https://jitpack.io'
            content {
                groups.jitpack.regex.each { includeGroupByRegex it }
                groups.jitpack.group.each { includeGroup it }
            }
        }
        google {
            content {
                groups.google.regex.each { includeGroupByRegex it }
                groups.google.group.each { includeGroup it }
            }
        }
        mavenCentral {
            content {
                groups.mavenCentral.regex.each { includeGroupByRegex it }
                groups.mavenCentral.group.each { includeGroup it }
            }
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        // Warnings are potential errors, so stop ignoring them
        // Ignore on the SDK for the moment
        kotlinOptions.allWarningsAsErrors = false
    }


    plugins.withId("com.vanniktech.maven.publish") {
        // Publish on s01.oss.sonatype.org
        //https://github.com/vanniktech/gradle-maven-publish-plugin#where-to-upload-to
        mavenPublishing {
            publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL, true)

            signAllPublications()
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir

}
